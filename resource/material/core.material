cubeTexture:skyboxT{
	path=skybox
}

material:car2{
	program=posDiffLight
	sampler{
		s_texture=car2.BMP
	}
}

material:skyboxM{
	program=skybox
	sampler{
		skybox=skyboxT
	}
	op{
		depthTest=true
		cullFace=true,front
	}
}
material:skyboxHdr1{
	program=skyboxHdr
	sampler{
		skybox=hdr/road.hdr
	}
	op{
		depthTest=true
		cullFace=true,front
	}
}
config{
	defferedGeoVs{
		layout(location = 0) in vec3 inPos;
		#ifdef HAS_MAP
			layout(location = 1) in vec2 inTexcoord;
			layout(location = 2) in vec3 inNormal;
			out vec2 texcoord;
		#else
			layout(location = 1) in vec3 inNormal;
		#endif
		layout (std140) uniform Matrixes
		{
			mat4 projMat;
			mat4 viewMat;
			mat4 shadowMat;
		};
		uniform mat4 modelMat;

		out vec3 worldPos;
		out vec3 worldNormal;
		#ifdef HAS_SHADOW
			out vec4 shadowPos;
			out vec3 shadowNormal;
		#endif
		
		#ifdef HAS_TAA
			layout (std140) uniform ScreenWH
			{
				float screenWidth;
				float screenHeight;
				
			};
			layout (std140) uniform Taa
			{
				int frameCount;
				int offsetIndex;
			};
			uniform mat4 preMvpMat;
			out vec4 preScreenPos;
			out vec4 curScreenPos;
			const vec2 Halton23[8] = vec2[8]
			(
				vec2(0.0f, -1.0f / 3.0f),
				vec2(-1.0f / 2.0f, 1.0f / 3.0f),
				vec2(1.0f / 2.0f, -7.0f / 9.0f),
				vec2(-3.0f / 4.0f, -1.0f / 9.0f),
				vec2(1.0f / 4.0f, 5.0f / 9.0f),
				vec2(-1.0f / 4.0f, -5.0f / 9.0f),
				vec2(3.0f / 4.0f, 1.0f / 9.0f),
				vec2(-7.0f / 8.0f, 7.0f / 9.0f)
			);
		#endif
		
		#ifdef HAS_NODE_ANIMATION
			layout (location = 3) in ivec4 boneIDs;
			layout (location = 4) in vec4 weights;
			layout(std140) uniform Bones
			{
				mat4 bones[128];
			};
		#endif
		void main(){
			mat4 mvMat = viewMat * modelMat;
			#ifdef HAS_MAP
				texcoord = inTexcoord;
			#endif
			vec4 animatedPos = vec4(inPos, 1.0);
			vec4 animatedNormal = vec4(inNormal, 0.0);
			#ifdef HAS_NODE_ANIMATION
				mat4 BoneTransform = bones[boneIDs[0]] * weights[0];
				BoneTransform += bones[boneIDs[1]] * weights[1];
				BoneTransform += bones[boneIDs[2]] * weights[2];
				BoneTransform += bones[boneIDs[3]] * weights[3];
				animatedPos = BoneTransform * animatedPos;
				animatedNormal = BoneTransform * animatedNormal;
			#endif
			
			worldNormal = vec3(transpose(inverse(mvMat)) * animatedNormal);
			worldPos = vec3(mvMat * animatedPos);
			#ifdef HAS_TAA
				preScreenPos = preMvpMat * animatedPos;
				float deltaWidth = 1.0f/screenWidth;
				float deltaHeight = 1.0f/screenHeight;
				vec2 jitter=vec2(Halton23[offsetIndex].x*deltaWidth,
				Halton23[offsetIndex].y*deltaHeight);
				mat4 jitterMat = projMat;
				jitterMat[2][0] += jitter.x;
				jitterMat[2][1] += jitter.y;
				gl_Position = jitterMat * mvMat * animatedPos;
				curScreenPos = projMat * mvMat * animatedPos;
			#else
				gl_Position = projMat * mvMat * animatedPos;
			#endif
			#ifdef HAS_SHADOW
				shadowPos = shadowMat * mvMat * animatedPos;
				shadowNormal = vec3(shadowMat * mvMat * animatedNormal);
			#endif
		}
	}
	defferedGeoFs{
		layout (location = 0) out vec4 outPosMap;
		layout (location = 1) out vec4 outNormalMap;
		layout (location = 2) out vec4 outAlbedoMap;
		in vec3 worldPos;
		in vec3 worldNormal;
		#ifdef HAS_TAA
			layout(location=3) out vec2 outVelocity;
			in vec4 preScreenPos;
			in vec4 curScreenPos;
		#endif
		#ifdef HAS_MAP
			in vec2 texcoord;
		#endif
		
		#ifdef HAS_SHADOW
			#ifdef HAS_TAA
				layout (location = 4) out float outShadowMap;
			#else
				layout (location = 3) out float outShadowMap;
			#endif
			in vec4 shadowPos;
			in vec3 shadowNormal;
			uniform sampler2DShadow shadowMap;
			vec2 poissonDisk[16] = vec2[]( 
			   vec2( -0.94201624, -0.39906216 ), 
			   vec2( 0.94558609, -0.76890725 ), 
			   vec2( -0.094184101, -0.92938870 ), 
			   vec2( 0.34495938, 0.29387760 ), 
			   vec2( -0.91588581, 0.45771432 ), 
			   vec2( -0.81544232, -0.87912464 ), 
			   vec2( -0.38277543, 0.27676845 ), 
			   vec2( 0.97484398, 0.75648379 ), 
			   vec2( 0.44323325, -0.97511554 ), 
			   vec2( 0.53742981, -0.47373420 ), 
			   vec2( -0.26496911, -0.41893023 ), 
			   vec2( 0.79197514, 0.19090188 ), 
			   vec2( -0.24188840, 0.99706507 ), 
			   vec2( -0.81409955, 0.91437590 ), 
			   vec2( 0.19984126, 0.78641367 ), 
			   vec2( 0.14383161, -0.14100790 ) 
			);
			// Returns a random number based on a vec3 and an int.
			float random(vec3 seed, int i){
				vec4 seed4 = vec4(seed,i);
				float dot_product = dot(seed4, vec4(12.9898,78.233,45.164,94.673));
				return fract(sin(dot_product) * 43758.5453);
			}
			float ShadowCalculation(vec4 fragPosLightSpace)
			{
				vec4 projCoords = fragPosLightSpace * 0.5 + 0.5;
				float cosNL=clamp(dot(vec3(0.0f,0.0f,1.0f),shadowNormal),0.0f,1.0f);
				float bias = 0.001f*tan(acos(cosNL));
				bias = clamp(bias,0.0f,0.01f);
				float visibility=1.0;
				projCoords.z -= bias*projCoords.w;
				for (int i=0;i<4;i++){
					int index = int(16.0*random(floor(worldPos*1000.0), i))%16;
					projCoords.xy += poissonDisk[index]/700.0;
					visibility -= 0.2 * ( 1.0 - textureProj(shadowMap, projCoords) );
				}
				return visibility;
			}
		#endif
		
		#ifdef HAS_ALBEDO_MAP
			uniform sampler2D albedoMap;
		#else
			uniform vec4 albedo;
		#endif
		
		#ifdef HAS_ARM_MAP
			uniform sampler2D armMap;
		#endif
		
		uniform float metallic;
		uniform float roughness;
		
		#ifdef HAS_NORMAL_MAP
			uniform sampler2D normalMap;
		#endif
		
		#ifdef HAS_NORMAL_MAP
			vec3 getNormalFromMap(sampler2D nmap,vec2 texcoord,vec3 worldPos,vec3 normal){
				vec3 tangentNormal = texture(nmap, texcoord).xyz * 2.0f - 1.0f;
				vec3 Q1 = dFdx(worldPos);
				vec3 Q2 = dFdy(worldPos);
				vec2 st1 = dFdx(texcoord);
				vec2 st2 = dFdy(texcoord);
				vec3 N = normalize(normal);
				vec3 T = normalize(Q1 * st2.t - Q2 * st1.t);
				vec3 B = -normalize(cross(N, T));
				mat3 TBN = mat3(T, B, N);
				return normalize(TBN * tangentNormal);
			}
		#endif
		void main(){
			outPosMap.rgb = worldPos;
			#ifdef HAS_ALBEDO_MAP
				outAlbedoMap.rgb = texture(albedoMap,texcoord).rgb;
			#else
				outAlbedoMap.rgb = albedo.rgb;
			#endif
			#ifdef HAS_NORMAL_MAP
				outNormalMap.rgb = getNormalFromMap(normalMap,texcoord,worldPos,worldNormal);
			#else
				outNormalMap.rgb = worldNormal;
			#endif
			
			#ifdef HAS_ARM_MAP
				vec3 arm = texture(armMap,texcoord).rgb;
				outNormalMap.a = metallic*arm.b;
				outAlbedoMap.a = roughness*arm.g;
			#else
				outNormalMap.a = metallic;
				outAlbedoMap.a = roughness;
			#endif
			#ifdef HAS_TAA
				// velocity
				vec2 newPos = ((curScreenPos.xy / curScreenPos.w) * 0.5 + 0.5);
				vec2 prePos = ((preScreenPos.xy / preScreenPos.w) * 0.5 + 0.5);
				outVelocity = newPos - prePos;
			#endif
			
			#ifdef HAS_SHADOW
				outShadowMap = 0.2 + ShadowCalculation(shadowPos);
			#endif
		}
	}
	
	defferedLightVs{
		#version 330 core
		layout(location = 0) in vec3 inPos;
		layout(location = 1) in vec2 inTexcoord;
		out vec2 texcoord;
		void main()
		{
		   gl_Position = vec4(inPos,1.0f);
		   texcoord = inTexcoord;
		}
	}
	
	defferedLightFs{
		const float PI = 3.14159265359f;
		out vec4 FragColor;
		in vec2 texcoord;

		uniform sampler2D posMap;//ao in a
		uniform sampler2D albedoMap;//roughness in a
		uniform sampler2D normalMap;//metallic in a
		uniform sampler2D ssaoMap;
		#ifdef HAS_IBL
			// IBL
			uniform samplerCube irrMap;
			uniform samplerCube prefilterMap;
			uniform sampler2D brdfLUT;
		#endif
		#ifdef HAS_SHADOW
			uniform sampler2D shadowResult;
		#endif
		
		layout (std140) uniform Lights
		{
			vec3 lightPositions[15];
			vec3 lightColors[15];
			int lightCounts;
		};
		
		
		uniform float clearCoatRoughness;

		/*Trowbridge-Reitz GGX normal distribution function
		*	N is normal,
		*	H is half vector
		*	
		*/
		float DistributionGGX(vec3 N, vec3 H, float roughness)
		{
			float a 	 = roughness*roughness;
			float a2     = a*a;
			float NdotH  = max(dot(N, H), 0.0f);
			float NdotH2 = NdotH*NdotH;
			
			float nom    = a2;
			float denom  = (NdotH2 * (a2 - 1.0f) + 1.0f);
			denom        = PI * denom * denom;
			
			return nom / denom;
		}
		
		/*Geometry function Schlick-GGX 
		*	k is remaping of roughness ,
		*	for direct lighting k=(a+1)2 / 8
		*	or IBL lighting k = a2 / 2
		*/
		float GeometrySchlickGGX(float NdotV, float roughness)
		{
			float r = (roughness + 1.0);
			float k = (r*r) / 8.0f;

			float num   = NdotV;
			float denom = NdotV * (1.0f - k) + k;
			
			return num / denom;
		}
		  
		float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
		{
			float NdotV = max(dot(N, V), 0.0f);
			float NdotL = max(dot(N, L), 0.0f);
			float ggx1 = GeometrySchlickGGX(NdotV, roughness);
			float ggx2 = GeometrySchlickGGX(NdotL, roughness);
			
			return ggx1 * ggx2;
		}
		
		/*Fresnel-Schlick approximation
		*	cosTheta being the dot product result between the surface's normal n and the halfway h (or view v) direction
		* 	vec3 F0 = vec3(0.04);
		*	F0 = mix(F0, surfaceColor.rgb, metalness);
		*/
		vec3 fresnelSchlick(float HoV, vec3 F0)
		{
			return F0 + (1.0f - F0) * pow(clamp(1.0f - HoV, 0.0f, 1.0f), 5.0f);
		}
		
		vec3 fresnelSchlickRoughness(float NoV, vec3 F0, float roughness)
		{
			return F0 + (max(vec3(1.0f - roughness), F0) - F0) * pow(clamp(1.0f - NoV, 0.0f, 1.0f), 5.0f);
		}
		
		/*
		*filament的F函数的实现
		*/
		vec3 F_Schlick(const vec3 f0, float VoH) {
			float f = pow(1.0 - VoH, 5.0);
			return f + f0 * (1.0 - f);
		}

		float F_Schlick(float f0, float f90, float VoH) {
			return f0 + (f90 - f0) * pow(1.0 - VoH,5.0);
		}
		/*
		*filament的G函数的实现
		*/
		float V_SmithGGXCorrelated(float NoV, float NoL, float roughness) {
			float a2 = roughness * roughness;
			float lambdaV = NoL * sqrt((NoV - a2 * NoV) * NoV + a2);
			float lambdaL = NoV * sqrt((NoL - a2 * NoL) * NoL + a2);
			return 0.5 / (lambdaV + lambdaL);
		}
		float V_SmithGGXCorrelatedFast(float NoV, float NoL, float roughness) {
			float a = roughness;
			float GGXV = NoL * (NoV * (1.0 - a) + a);
			float GGXL = NoV * (NoL * (1.0 - a) + a);
			return 0.5 / (GGXV + GGXL);
		}
		//用于clear coat layer的G函数实现，DF都与标准模型一样计算
		float V_Kelemen(float LoH) {
			return 0.25 / (LoH * LoH);
		}
		/*
		*filament的D函数的实现
		*/
		float D_GGX(float NoH, float roughness) {
			float a = NoH * roughness;
			float k = roughness / (1.0 - NoH * NoH + a * a);
			return k * k * (1.0 / PI);
		}
		/*
		*filament的D函数的mediump版本的实现
		*/
		#define MEDIUMP_FLT_MAX    65504.0
		#define saturateMediump(x) min(x, MEDIUMP_FLT_MAX)
		float D_GGX(float roughness, float NoH, const vec3 n, const vec3 h) {
			vec3 NxH = cross(n, h);
			float a = NoH * roughness;
			float k = roughness / (dot(NxH, NxH) + a * a);
			float d = k * k * (1.0 / PI);
			return saturateMediump(d);
		}
		
		vec3 directLight(float roughness,float metallic,vec3 F0,vec3 albedo,vec3 worldPos,vec3 normal,vec3 lightPos,vec3 lightColor){
			vec3 Lo = vec3(0.0f);
			vec3 camPos = vec3(0.0f,0.0f,0.0f);
			vec3 V = camPos-worldPos;
			// calculate per-light radiance
			vec3 L = normalize(lightPos - worldPos);
			vec3 H = normalize(V + L);
			float distance    = length(lightPos - worldPos);
			float attenuation = 1.0f/(distance*distance);
			vec3 radiance     = lightColor * attenuation;        
			
			// cook-torrance brdf
			float NoV = max(dot(normal, V), 0.0f);
			float NoL = max(dot(normal, L), 0.0f);
			float NDF = DistributionGGX(normal, H, roughness);        
			float G   = GeometrySmith(normal, V, L, roughness);		
			vec3 F    = fresnelSchlick(max(dot(H, V), 0.0f), F0);
			float denominator = 4.0f * max(dot(normal, V), 0.0f) * max(dot(normal, L), 0.0f) + 0.0001f;
			vec3 numerator    = NDF * G * F;
			vec3 specular     = numerator / denominator;
			
			#ifdef HAS_CLEAR_COAT
				//clear coat DFG
				float NDFc = DistributionGGX(normal, H, 0.1);
				float Gc = GeometrySmith(normal, V, L, 0.1);
				vec3 Fc    = fresnelSchlick(max(dot(H, V), 0.0f), vec3(0.04));
				vec3 numeratorC    = NDFc * Gc * Fc;
				vec3 specularC     = numeratorC / denominator; 		
			#endif
			
			vec3 kS = F;
			vec3 kD = vec3(1.0f) - kS;
			kD *= 1.0f - metallic;	  
			
			  
				
			// add to outgoing radiance Lo
			float NdotL = max(dot(normal, L), 0.0f);
			#ifdef HAS_CLEAR_COAT
				Lo += ((kD * albedo / PI + specular)*(vec3(1.0)-Fc)*NdotL + specularC*NdotL)*radiance;
			#else
				Lo += (kD * albedo / PI + specular*NdotL)*radiance;
			#endif
			return Lo;
		}
		
		vec3 envLight(float ao,float roughness,float metallic,vec3 F0,vec3 albedo,vec3 worldPos,vec3 normal,samplerCube irrMap,samplerCube prefilterMap,sampler2D brdfLUT){
			vec3 V = normalize(vec3(0.0f,0.0f,0.0f) - worldPos);
			// ambient lighting (we now use IBL as the ambient term)
			vec3 F = fresnelSchlickRoughness(max(dot(normal, V), 0.0f), F0, roughness);
			vec3 R = reflect(-V, normal);
			vec3 kS = F;
			vec3 kD = 1.0f - kS;
			kD *= (1.0f - metallic);	  
			
			vec3 irradiance = texture(irrMap, normal).rgb;
			vec3 diffuse      = irradiance * albedo;
			
			// sample both the pre-filter map and the BRDF lut and combine them together as per the Split-Sum approximation to get the IBL specular part.
			const float MAX_REFLECTION_LOD = 4.0;
			vec3 prefilteredColor = textureLod(prefilterMap, R,  roughness * MAX_REFLECTION_LOD).rgb;    
			vec2 brdf  = texture(brdfLUT, vec2(max(dot(normal, V), 0.0), roughness)).rg;
			vec3 specular = prefilteredColor * (F * brdf.x + brdf.y);
			#ifdef HAS_CLEAR_COAT
				vec3 Fc = fresnelSchlickRoughness(max(dot(normal, V), 0.0f), vec3(0.04), 0.1);
				vec3 specularC = prefilteredColor * (Fc * brdf.x + brdf.y);
				vec3 l_fc = vec3(1.0)-Fc;
				vec3 sq1_fc = l_fc*l_fc;
				vec3 ambient = (kD * diffuse*l_fc + specular*sq1_fc + specularC)*ao;
			#else
				vec3 ambient = (kD * diffuse+specular)*ao;
			#endif
			return ambient;
		}
		

		void main()
		{             
			// retrieve data from G-buffer
			vec4 posRgba = texture(posMap, texcoord);
			vec4 normalRgba = texture(normalMap, texcoord);
			vec4 albedoRgba = texture(albedoMap, texcoord);
			float ssao = texture(ssaoMap,texcoord).r;
			vec3 worldPos = posRgba.rgb;
			vec3 normal = normalRgba.rgb;
			vec3 albedo = albedoRgba.rgb;
			float metallic = normalRgba.a;
			float roughness = albedoRgba.a;
			float ao = ssao;
			#ifdef HAS_SHADOW
				ao *= texture(shadowResult, texcoord).r;
			#endif
			// calculate reflectance at normal incidence; if dia-electric (like plastic) use F0 
			// of 0.04 and if it's a metal, use the albedo color as F0 (metallic workflow)    
			vec3 F0 = vec3(0.04); 
			F0 = mix(F0, albedo, metallic);
			vec3 dirColor = vec3(0.0);
			for(int i=0; i<lightCounts; ++i){
				dirColor += directLight(roughness,metallic,F0,albedo,worldPos,normal,lightPositions[i],lightColors[i]);
			}
			vec3 envColor = envLight(ao,roughness,metallic,F0,albedo,worldPos,normal,irrMap,prefilterMap,brdfLUT);
			
			vec3 color = dirColor + envColor;
			
			FragColor = vec4(color, 1.0);
		}  
	}
}